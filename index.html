<!DOCTYPE html>
<html>
<head>
    <title>Trabalho 2 de CG</title>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <style>
        body {
            margin: 0;
            padding: 0;
            overflow: hidden;
            background-color: #000;
        }
        #game-surface {
            display: block;
            width: 100vw;
            height: 100vh;
            background-color: #000;
        }
    </style>
</head>
<body>
    <canvas id="game-surface"></canvas>

    <!-- Vertex Shader -->
    <script id="vertShader" type="x-shader/x-vertex">
        precision mediump float;
        attribute vec2 position;
        void main() {
            gl_Position = vec4(position, 0.0, 1.0);
        }
    </script>

    <!-- Fragment Shader-->
    <script id="fragShader" type="x-shader/x-fragment">
        precision highp float;
        uniform float time;
        uniform vec2 resolution;
        
        float circle(vec2 p, float r) {
            return length(p) - r;
        }
        
        float hexagon(vec2 p, float r) {
            vec2 q = abs(p);
            return max((q.x + q.y*0.57735), q.y*1.1547) - r;
        }
        
        float star(vec2 p, float r, int n) {
            float an = 3.141593/float(n);
            float en = 3.141593/mix(1.0,3.0,smoothstep(0.2,0.8,sin(time)));
            vec2 acs = vec2(cos(an),sin(an));
            vec2 ecs = vec2(cos(en),sin(en));
            
            float bn = mod(atan(p.x,p.y),2.0*an) - an;
            p = length(p)*vec2(cos(bn),abs(sin(bn)));
            p -= r*acs;
            p += ecs*clamp(-dot(p,ecs),0.0,r*0.5/acs.y);
            return length(p)*sign(p.x);
        }
        
        vec2 rotate(vec2 p, float a) {
            return mat2(cos(a),-sin(a),sin(a),cos(a)) * p;
        }
        
        float fractal1(vec2 p) { //Hexagono
            p = rotate(p, time*0.3);
            float d = hexagon(p, 0.3);
            for(int i=0; i<3; i++) {
                p = abs(p) - 0.2;
                p = rotate(p, time*0.2);
                d = min(d, hexagon(p, 0.15));
            }
            return d;
        }
        
        float fractal2(vec2 p) { //Estrela 5 pontas
            p = rotate(p, -time*0.2);
            float d = star(p, 0.4, 5);
            p = abs(p) - 0.15;
            d = max(d, -star(p, 0.25, 5));
            return d;
        }
        
        float fractal3(vec2 p) { //Círculos
            p = rotate(p, time*0.4);
            float d = circle(p, 0.3);
            for(int i=0; i<4; i++) {
                p = abs(p) - 0.1 - 0.05*sin(time);
                p = rotate(p, 0.78 + time*0.1);
                d = min(d, circle(p, 0.15));
            }
            return d;
        }
        
        float fractal4(vec2 p) { //6 Círculos
            p = rotate(p, time*0.25);
            float d = 1e5;
            for(int i=0; i<6; i++) {
                float a = 6.2831*float(i)/6.0;
                vec2 q = p - 0.2*vec2(cos(a),sin(a));
                d = min(d, circle(q, 0.1 + 0.05*sin(time + float(i))));
            }
            return d;
        }
        
        float fractal5(vec2 p) { //Heágono
            p = rotate(p, time*0.15);
            float d = hexagon(p, 0.25);
            p = abs(p) - 0.15;
            p = rotate(p, 1.57);
            d = max(d, -hexagon(p, 0.2));
            return d;
        }
        
        float fractal6(vec2 p) { //Estrela 7 pontas
            p = rotate(p, time*0.35);
            float d = star(p, 0.35, 7);
            for(int i=0; i<3; i++) {
                p = abs(p) - 0.1;
                p = rotate(p, 0.5 + time*0.05);
                d = min(d, star(p, 0.15, 7));
            }
            return d;
        }
        
        float fractal7(vec2 p) { //8 Estrelas
            p = rotate(p, time*0.2);
            float d = 1e5;
            
            float center = circle(p, 0.15 + 0.03*sin(time*2.0));
            
            for(int i=0; i<8; i++) {
                float angle = 6.2831*float(i)/8.0;
                vec2 q = rotate(p - vec2(0.3,0.0), angle);
                float petal = star(q, 0.15, 4);
                d = min(d, petal);
            }
            
            return min(d, center);
        }
        
        float fractal8(vec2 p) { //12 hexagonos
            p = rotate(p, time*0.18);
            float d = 1e5;
            
            float ring = abs(circle(p, 0.35)) - 0.05;
            
            for(int i=0; i<12; i++) {
                float angle = 6.2831*float(i)/12.0;
                vec2 q = rotate(p - vec2(0.25,0.0), angle + time*0.1);
                float element = hexagon(q, 0.08 + 0.02*sin(time*3.0 + float(i)));
                d = min(d, element);
            }
            
            return min(d, ring);
        }
        
        void main() {
            vec2 uv = (2.0*gl_FragCoord.xy-resolution.xy)/resolution.y;
            
            vec2 pos[8];
            pos[0] = vec2(sin(time*0.3)*0.6, cos(time*0.2)*0.5);
            pos[1] = vec2(-cos(time*0.4)*0.5, -sin(time*0.3)*0.6);
            pos[2] = vec2(sin(time*0.5)*0.4, -cos(time*0.6)*0.4);
            pos[3] = vec2(-cos(time*0.2)*0.7, sin(time*0.4)*0.3);
            pos[4] = vec2(sin(time*0.7)*0.5, cos(time*0.5)*0.5);
            pos[5] = vec2(-cos(time*0.3)*0.4, -sin(time*0.7)*0.6);
            pos[6] = vec2(cos(time*0.5)*0.5, sin(time*0.4)*0.4); 
            pos[7] = vec2(-sin(time*0.6)*0.5, cos(time*0.3)*0.5);
            
            float d[8];
            d[0] = smoothstep(0.02, 0.0, fractal1(uv-pos[0]));
            d[1] = smoothstep(0.02, 0.0, fractal2(uv-pos[1]));
            d[2] = smoothstep(0.02, 0.0, fractal3(uv-pos[2]));
            d[3] = smoothstep(0.02, 0.0, fractal4(uv-pos[3]));
            d[4] = smoothstep(0.02, 0.0, fractal5(uv-pos[4]));
            d[5] = smoothstep(0.02, 0.0, fractal6(uv-pos[5]));
            d[6] = smoothstep(0.02, 0.0, fractal7(uv-pos[6])); 
            d[7] = smoothstep(0.02, 0.0, fractal8(uv-pos[7])); 
            
            vec3 col = vec3(0.0);
            col += d[0] * vec3(0.8,0.3,0.4); // Vermelho
            col += d[1] * vec3(0.2,0.7,0.8); // Azul
            col += d[2] * vec3(0.9,0.8,0.1); // Amarelo
            col += d[3] * vec3(0.3,0.8,0.3); // Verde
            col += d[4] * vec3(0.8,0.4,0.8); // Roxo
            col += d[5] * vec3(0.1,0.5,0.9); // Azul claro
            col += d[6] * vec3(1.0,0.6,0.0); // Laranja
            col += d[7] * vec3(0.5,0.2,0.7); // Lilás
            
            float overlap = min(1.0, (d[0]+d[1]+d[2]+d[3]+d[4]+d[5]+d[6]+d[7])*0.5);
            col += overlap * 0.4 * vec3(1.0,1.0,1.0);
            
            gl_FragColor = vec4(col, 1.0);
        }
    </script>
        
    <script src="Timer.js"></script>
    <script src="Shader.js"></script>
    <script src="Mesh.js"></script>
    <script src="main.js"></script>
</body>
</html>